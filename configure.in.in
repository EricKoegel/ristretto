dnl
dnl Copyright (c) 2006-2009
dnl         The Xfce development team. All rights reserved.
dnl
dnl Originally written for Xfce by Benedikt Meurer <benny@xfce.org>
dnl Modified by Stephan Arts <stephan@xfce.org>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([ristretto_version_major], [0])
m4_define([ristretto_version_minor], [3])
m4_define([ristretto_version_micro], [6])
m4_define([ristretto_version_build], [r@REVISION@])
m4_define([ristretto_version_tag], []) # Leave empty for releases
m4_define([ristretto_version], [ristretto_version_major().ristretto_version_minor().ristretto_version_micro()ifelse(ristretto_version_tag(), [], [], [ristretto_version_tag()-ristretto_version_build()])])

dnl *******************************************
dnl *** Debugging support for SVN snapshots ***
dnl *******************************************
m4_define([ristretto_debug_default], [ifelse(ristretto_version_tag(), [git], [full], [minimum])])

AC_COPYRIGHT([Copyright (c) 2006-2012
        The Xfce development team. All rights reserved.

Written for Xfce by Stephan Arts <stephan@xfce.org>.])

AC_INIT([ristretto], [ristretto_version], [])
AC_PREREQ([2.50])

RISTRETTO_VERSION=ristretto_version
AM_INIT_AUTOMAKE([ristretto], [$RISTRETTO_VERSION])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

dnl check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX
AM_CONDITIONAL([HAVE_CYGWIN], [test "`uname | grep \"CYGWIN\"`" != ""])


dnl check for standard header files
AC_PROG_CC
AC_PROG_INTLTOOL([0.31], [no-xml])


AC_HEADER_STDC

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

XDT_CHECK_PACKAGE([LIBEXIF], [libexif], [0.6.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.20.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.24.0])
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.24.0])
XDT_CHECK_PACKAGE([GOBJECT], [gobject-2.0], [2.24.0])
XDT_CHECK_PACKAGE([GIO], [gio-2.0], [2.18.0])
XDT_CHECK_PACKAGE([DBUS_GLIB], [dbus-glib-1], [0.34])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.8.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-1], [4.8.0])

XDT_CHECK_PACKAGE([EXO], [exo-1], [0.4.0])

XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [4.8.0])

XDT_CHECK_PACKAGE([CAIRO], [cairo], [1.8.0])

XDT_CHECK_LIBX11()

AC_CHECK_LIBM
AC_SUBST(LIBM)

dnl *******************************
dnl *** Check for documentation ***
dnl *******************************
AC_ARG_ENABLE([gen-doc], [AC_HELP_STRING([--enable-gen-doc],
              [Generate HTML documentation (requires gnome-doc-utils) [default=no]])],,
              [enable_gen_doc=no])
if test x"$enable_gen_doc" = x"yes"; then
    XDT_CHECK_PACKAGE([GNOME_DOC_UTILS], [gnome-doc-utils], [0.19.2],
    [
        AC_PATH_PROG([ITSTOOL], [itstool], [no])
        if test x"$ITSTOOL" = x"no"; then
            enable_gen_doc=no
        fi

        AC_PATH_PROG([GNOME_DOC_TOOL], [gnome-doc-tool], [no])
        if test x"$GNOME_DOC_TOOL" = x"no"; then
            enable_gen_doc=no
        fi

        AC_PATH_PROG([XML2PO], [xml2po], [no])
        if test x"$XML2PO" = x"no"; then
            enable_gen_doc=no
        fi
    ],
    [
        enable_gen_doc=no
    ])

    if test x"$enable_gen_doc" = x"no"; then
        AC_MSG_ERROR([Compiling with --enable-gen-doc but no suitable gnome-doc-utils found.])
    fi
fi
AM_CONDITIONAL([GENERATE_DOCUMENTATION], [test x"$enable_gen_doc" = x"yes"])



dnl check for debugging support
XDT_FEATURE_DEBUG

AC_OUTPUT([
Makefile
po/Makefile.in
src/Makefile
docs/Makefile
docs/manual/Makefile
docs/manual/po/Makefile
docs/manual/media/Makefile
icons/Makefile
icons/16x16/Makefile
icons/22x22/Makefile
icons/24x24/Makefile
icons/32x32/Makefile
icons/36x36/Makefile
icons/48x48/Makefile
icons/scalable/Makefile
])

echo "----------------------------------------"
echo "$PACKAGE $VERSION"
echo ""
echo "The binary will be installed in $prefix/bin"
echo "----------------------------------------"
